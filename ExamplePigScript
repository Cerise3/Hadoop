ratings = LOAD '/user/maria_dev/ml-100k/u.data' AS (userID:int, movieID:int, rating:int, ratingTime:int);

metadata = LOAD '/user/maria_dev/ml-100k/u.item' USING PigStorage('|')
	AS (mID:int, mTitle:chararray, rDate:chararray, vDate:chararray, imdbLink:chararray);

nameLookup = FOREACH metadata GENERATE mID, mTitle, 
	ToUnixTime(ToDate(rDate, 'dd-MMM-yyyy')) AS rTime;

ratingByMovie = GROUP ratings BY movieID;

avgRatings = FOREACH ratingByMovie GENERATE group AS movieID, AVG(ratings.rating) AS avgRating;

fiveStar = FILTER avgRatings BY avgRating > 4.0;

fiveStarData = JOIN fiveStar BY movieID, nameLookup BY mID;

oldestFiveStar = ORDER fiveStarData BY nameLookup::rTime;

DUMP oldestFiveStar;